LANG "en"
HEAD
	LOAD "_ROOT/../cms/shared/head"
	NAME "Login | kytschBASIC CMS"
END HEAD
BODY
	LOAD "_ROOT/../cms/shared/menu"
	MAIN
		DIV "kb-row","main-content"
			DIV "kb-col kb-col-24","main-content-col"
				IFNTE _POST("btn_save") THEN
					SESSCLEAR "user"
					DEF required_fields&=("username", "password")
					DEF missing_fields%=0
					
					FOR field$ IN required_fields&
						IF _POST(field$)="" THEN
							LET missing_fields%=1
						END IF
					NEXT

					IF missing_fields%=1 THEN
						DIV "alert"
							PRINT "Missing required fields"
						END DIV
					ELSE
						DEF user&=null
						DATA
							DOPEN "kytschBASIC-CMS"
							DREAD "users"
							DSELECT "id, username, nickname"	
							DWHERE "username=:username"
							DBIND username=_POST("username")
							DFETCH user&
						END DATA
						
						IF user& THEN
							DATA
								DOPEN "kytschBASIC-CMS"
								DREAD "users"
								DSELECT "password"	
								DWHERE "id=:id"
								DBIND id=user&[0]["id"]
								DFETCH password&
							END DATA
							DEF verify%=HASHVERIFY(_POST("password"), password&[0]["password"])
							IF verify% THEN
								SESSWRITE "user", user&
								GOTO "/kb-cms"
							ELSE
								DIV "alert"
									PRINT "Invalid login"
								END DIV
							END IF
						ELSE
							DIV "alert"
								PRINT "Invalid login"
							END DIV
						END IF
					END IF
				END IF
				DIV
					FORM "form_save", "POST"
						DIV "form-header"
							PRINT "Login"
						END DIV
						DIV "form-body"
							DIV "form-group"
								SWRITE
									PRINT "Username"
									PRINT "*", "required"
								END SWRITE
								TEXTINPUT "username",,"form-input", "Please enter a username",,true
							END DIV
							DIV "form-group"
								SWRITE
									PRINT "Password"
									PRINT "*", "required"
								END SWRITE
								PASSWORDINPUT "password",,"form-input", "Please enter a password",,true
							END DIV
						END DIV
						DIV "form-footer"
							DIV
								PRINT " required fields*", "required"
							END DIV
							DIV "form-footer-buttons"
								BUTTON SUBMIT "btn_save",,,, "Login"
							END DIV
						END DIV
					END FORM
				END DIV
			END DIV
		END DIV
		LOAD "_ROOT/../cms/shared/footer"
	END MAIN
END BODY
END
