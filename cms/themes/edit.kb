LANG "en"
HEAD
	LOAD "_ROOT/../cms/shared/head"
	NAME "Edit the theme | kytschBASIC CMS"
END HEAD
BODY
	LOAD "_ROOT/../cms/shared/menu"
	MAIN
		LOAD "_ROOT/../cms/shared/toolbar"
		DIV "kb-row","main-content"
			DIV "kb-col kb-col-24","main-content-col"
				IF session_message$ THEN
					SESSCLEAR "session_message"
					DIV "alert alert-info"
						PRINT session_message$
					END DIV
				END IF
				IFNTE _POST("btn_save") THEN
					DEF is_default%=0
					IFNTE _POST("is_default") THEN 
						LET is_default%=1
					END IF
					DATA
						DOPEN "kytschBASIC-CMS"
						DREAD "themes"
						DSET "name=:name, folder=:folder, is_default=:is_default, type=:type, updated_by=:user_id"
						DBIND id=_UVARS("id"), name=_POST("name"), folder=_POST("folder"), type=_POST("type"), is_default=is_default%, user_id=valid_user&[0]["id"]
						DWHERE "id=:id"
						DEXEC
					END DATA
					SESSWRITE "session_message", "Entry has been updated"
					GOTO "/kb-cms/themes/edit/" + _UVARS("id")
				END IF
				IFNTE _POST("btn_delete") THEN
					DATA
						DOPEN "kytschBASIC-CMS"
						DREAD "themes"
						DSET "deleted_by=:user_id, updated_by=:user_id, deleted_at=NOW(), updated_at=NOW(), status='deleted'"
						DBIND id=_UVARS("id"), user_id=valid_user&[0]["id"]
						DWHERE "id=:id"
						DEXEC
					END DATA
					SESSWRITE "session_message", "Entry has been marked as deleted"
					GOTO "/kb-cms/themes/edit/" + _UVARS("id")
				END IF
				IFNTE _POST("btn_recover") THEN
					DATA
						DOPEN "kytschBASIC-CMS"
						DREAD "themes"
						DSET "deleted_by=NULL, updated_by=:user_id, deleted_at=NULL, updated_at=NOW(), status='active'"
						DBIND id=_UVARS("id"), user_id=valid_user&[0]["id"]
						DWHERE "id=:id"
						DEXEC
					END DATA
					SESSWRITE "session_message", "Entry has been recovered"
					GOTO "/kb-cms/themes/edit/" + _UVARS("id")
				END IF
				IFNTE _POST("btn_active") THEN
					DATA
						DOPEN "kytschBASIC-CMS"
						DREAD "themes"
						DSET "status=:status"
						DBIND id=_UVARS("id"), status=_POST("btn_active")
						DWHERE "id=:id"
						DEXEC
					END DATA
					SESSWRITE "session_message", "Entry has been marked as " + _POST("btn_active")
					GOTO "/kb-cms/themes/edit/" + _UVARS("id")
				END IF
				DATA
					DOPEN "kytschBASIC-CMS"
					DREAD "themes"
					DSELECT "*"	
					DWHERE "id=:id"
					DBIND id=_UVARS("id")
					DFETCH data&
				END DATA
				HEADING 2
					PRINT "Edit the theme"
				END HEADING 2
				IF data&[0]["deleted_at"] THEN
					DIV "alert alert-danger"
						PRINT "Entry marked as deleted"
					END DIV
				END IF
				FORM "form_save", "POST",,,true
					DIV "page-toolbar"
						DIV "kb-col-12"
							DIV
								LINK "/kb-cms/themes","Back to the list","btn-icon"
									IMAGE cms_theme&[0]["folder"] + "/imgs/arrow-left.svg", "Go back"
									PRINT "Back"
								END LINK
							END DIV
							DIV
								LINK "/kb-cms/themes/add","Create a page","btn-icon"
									IMAGE cms_theme&[0]["folder"] + "/imgs/file-earmark-plus.svg", "Add a new entry"
									PRINT "Add"
								END LINK
							END DIV
						END DIV
						DIV "kb-col-12 box-footer-buttons"
							IF data&[0]["deleted_at"] THEN
								BUTTONSUBMIT "btn_recover","btn-icon"
									IMAGE cms_theme&[0]["folder"] + "/imgs/recycle.svg", "Recover the entry"
									PRINT "Recover"
								END BUTTON
							ELSE
								BUTTONSUBMIT "btn_delete","btn-icon"
									IMAGE cms_theme&[0]["folder"] + "/imgs/trash.svg", "Mark the entry as deleted"
									PRINT "Delete"
								END BUTTON
							END IF
							IF data&[0]["status"]="active" THEN
								BUTTONSUBMIT "btn_active","btn-icon",,"inactive"
									IMAGE cms_theme&[0]["folder"] + "/imgs/cloud-minus.svg", "Mark as inactive"
									PRINT "Deactive"
								END BUTTON
							ELSE
								BUTTONSUBMIT "btn_active","btn-icon",,"active"
									IMAGE cms_theme&[0]["folder"] + "/imgs/cloud-check.svg", "Mark as active"
									PRINT "Active"
								END BUTTON
							END IF
							BUTTONSUBMIT "btn_save","btn-icon"
								IMAGE cms_theme&[0]["folder"] + "/imgs/floppy.svg", "Save the entry"
								PRINT "Save"
							END BUTTON
						END DIV
					END DIV
					DIV "box"
						DIV "box-header"
							PRINT "Details"
						END DIV
						DIV "box-body"
							DIV "input-group"
								SWRITE
									PRINT "Name"
									PRINT "*", "required"
								END SWRITE
								TEXTINPUT "name",data&[0]["name"],, "Please enter a name",,true
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Folder"
									PRINT "*", "required"
								END SWRITE
								TEXTINPUT "folder",data&[0]["folder"],, "Please enter a folder",,true
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Type"
								END SWRITE
								SELECTINPUT "type",, "form-input", "Please select a type"
									SELECTINPUTOPT "cms", "CMS", data&[0]["type"]
									SELECTINPUTOPT "website", "Website", data&[0]["type"]
								END SELECTINPUT
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Is default theme?"
								END SWRITE
								CHECKINPUT "is_default",1,,"Set the theme to default",,,data&[0]["is_default"]
							END DIV
						END DIV
						DIV "box-footer"
							DIV
								PRINT " required fields*", "required"
							END DIV
							DIV "box-footer-buttons"
								BUTTONSUBMIT "btn_save","btn-icon"
									IMAGE cms_theme&[0]["folder"] + "/imgs/floppy.svg", "Save button"
									PRINT "Save"
								END BUTTON
							END DIV
						END DIV						
					END DIV
				END FORM
			END DIV
		END DIV
		LOAD "_ROOT/../cms/shared/footer"
	END MAIN
END BODY
END
