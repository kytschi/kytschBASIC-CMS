LANG "en"
HEAD
	LOAD "_ROOT/../cms/shared/head"
	NAME "Add a theme | kytschBASIC CMS"
END HEAD
BODY
	LOAD "_ROOT/../cms/shared/menu"
	MAIN
		LOAD "_ROOT/../cms/shared/toolbar"
		DIV "kb-row","main-content"
			DIV "kb-col kb-col-24","main-content-col"
				IF session_message$ THEN
					SESSCLEAR "session_message"
					DIV "alert alert-info"
						PRINT session_message$
					END DIV
				END IF
				IFNTE _POST("btn_save") THEN
					DEF is_default%=0
					IFNTE _POST("is_default") THEN 
						LET is_default%=1
					END IF
					DATA
						DOPEN "kytschBASIC-CMS"
						DREAD "themes"
						DINSERT "name=:name, folder=:folder, is_default=:is_default, type=:type, created_by=:user_id, updated_by=:user_id"
						DBIND name=_POST("name"), folder=_POST("folder"), type=_POST("type"), is_default=is_default%, user_id=valid_user&[0]["id"]
						DEXEC
					END DATA
					DIV "alert alert-info"
						PRINT "Entry has been created"
					END DIV
				END IF
				HEADING 2
					PRINT "Add a theme"
				END HEADING 2
				FORM "form_save", "POST"
					DIV "page-toolbar"
						DIV "kb-col-12"
							DIV
								LINK "/kb-cms/themes","Back to the list","btn-icon"
									IMAGE "/themes/cms/default/imgs/arrow-left.svg", "Back button"
									PRINT "Back"
								END LINK
							END DIV
						END DIV
						DIV "kb-col-12 box-footer-buttons"
							BUTTONSUBMIT "btn_save","btn-icon"
								IMAGE "/themes/cms/default/imgs/floppy.svg", "Save button"
								PRINT "Save"
							END BUTTON
						END DIV
					END DIV
					DIV "box"
						DIV "box-header"
							PRINT "Details"
						END DIV
						DIV "box-body"
							DIV "input-group"
								SWRITE
									PRINT "Name"
									PRINT "*", "required"
								END SWRITE
								TEXTINPUT "name",,, "Please enter a name",,true
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Folder"
									PRINT "*", "required"
								END SWRITE
								TEXTINPUT "folder",,, "Please enter a folder",,true
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Type"
								END SWRITE
								SELECTINPUT "type",, "form-input", "Please select a type"
									SELECTINPUTOPT "cms", "CMS"
									SELECTINPUTOPT "website", "Website"
								END SELECTINPUT
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Is default theme?"
								END SWRITE
								CHECKINPUT "is_default",,, "Set the theme to default"
							END DIV
						END DIV
						DIV "box-footer"
							DIV
								PRINT " required fields*", "required"
							END DIV
							DIV "box-footer-buttons"
								BUTTONSUBMIT "btn_save","btn-icon"
									IMAGE "/themes/cms/default/imgs/floppy.svg", "Save button"
									PRINT "Save"
								END BUTTON
							END DIV
						END DIV						
					END DIV
				END FORM
			END DIV
		END DIV
		LOAD "_ROOT/../cms/shared/footer"
	END MAIN
END BODY
END
