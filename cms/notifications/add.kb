LANG "en"
HEAD
	LOAD "_ROOT/../cms/shared/head"
	NAME "Create a notification | kytschBASIC CMS"
END HEAD
BODY
	LOAD "_ROOT/../cms/shared/menu"
	MAIN
		LOAD "_ROOT/../cms/shared/toolbar"
		DIV "kb-row","main-content"
			DIV "kb-col kb-col-24","main-content-col"
				IF session_message$ THEN
					SESSCLEAR "session_message"
					DIV "alert alert-info"
						PRINT session_message$
					END DIV
				END IF
				IFNTE _POST("btn_save") THEN
					DATA
						DOPEN "kytschBASIC-CMS"
						DREAD "notifications"
						DINSERT "title=:title, content=:content, user_id=:for_user_id, created_by=:user_id, updated_by=:user_id"
						DBIND title=_POST("title"), content=_POST("content"), for_user_id=_POST("user_id"), user_id=valid_user&[0]["id"]
						DEXEC
					END DATA
					SESSWRITE "session_message", "Entry has been created"
					GOTO "/kb-cms/notifications/add"
				END IF
				HEADING 2
					PRINT "Create a notification"
				END HEADING 2
				FORM "form_save", "POST"
					DIV "page-toolbar"
						DIV "kb-col-12"
							DIV
								LINK "/kb-cms/notifications","Back to the list","btn-icon"
									IMAGE "/themes/cms/default/imgs/arrow-left.svg", "Back button"
									PRINT "Back"
								END LINK
							END DIV
						END DIV
						DIV "kb-col-12 box-footer-buttons"
							BUTTONSUBMIT "btn_save","btn-icon"
								IMAGE "/themes/cms/default/imgs/floppy.svg", "Save button"
								PRINT "Save"
							END BUTTON
						END DIV
					END DIV
					DIV "box"
						DIV "box-header"
							PRINT "Details"
						END DIV
						DIV "box-body"
							DIV "input-group"
								SWRITE
									PRINT "Title"
									PRINT "*", "required"
								END SWRITE
								TEXTINPUT "title",,, "Please enter a title",,true
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Person"
								END SWRITE
								DATA
									DOPEN "kytschBASIC-CMS"
									DREAD "users"
									DSELECT "*"					
									DSORT "users.nickname ASC"
									DFETCH users&
								END DATA
								SELECTINPUT "user_id",, "form-input", "Please select a user"
									SELECTINPUTOPT "", "No user"
									FOR user& IN users&
										SELECTINPUTOPT user&["id"], user&["nickname"]
									NEXT
								END SELECTINPUT
							END DIV
							DIV "input-group"
								SWRITE
									PRINT "Message"
								END SWRITE
								TEXTAREA "content",,, "Please enter some content if need be"
							END DIV
						END DIV
						DIV "box-footer"
							DIV
								PRINT " required fields*", "required"
							END DIV
							DIV "box-footer-buttons"
								BUTTONSUBMIT "btn_save","btn-icon"
									IMAGE "/themes/cms/default/imgs/floppy.svg", "Save button"
									PRINT "Save"
								END BUTTON
							END DIV
						END DIV						
					END DIV
				END FORM
			END DIV
		END DIV
		LOAD "_ROOT/../cms/shared/footer"
	END MAIN
END BODY
END
